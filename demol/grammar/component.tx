//Device
ComponentModel:
    component=Component
;

Component:
    Board | Peripheral
;


// Board meta-model
Board:
    'Board' name=ID
	(
	    ("cpu" ":" cpu=CPU)
        ("memory" ":" memory=Memory)
	    ("vcc" ":" vcc=FLOAT)
	    ("pins" ":" "-" pins*=Pin["-"])
	    ("ioVcc" ":" vcc=FLOAT)?
	    ('networking' ':' '-' networking*=NetworkInterface['-'])?
	    ('bluetooth' ':' bluetooth=Bluetooth)?
     )#
    'end'
;

Peripheral:
    'Peripheral' name=ID
	(
      ("type" ":" type=PeripheralType)
	  ("pins" ":" "-" pins*=Pin["-"])
	  ("vcc" ":" vcc=FLOAT)
    )#
    'end'
;

PeripheralType:
	"Sensor" | "Actuator" | "Other"
;

Pin:
	PowerPin | IOPin
;

PowerPin:
	"power:"
	(
        ("name:" name=ID)
	    ("number:" number=INT)
	    ("type:" type=PowerType)
    )#
;

PowerType:
	"GND"   |
    "5V"    |
    "3V3"   |
    "12V"
;

IOPin:
	"io_pin:"
	(
        ("functions:" funcs*=PinFunction[","])
        ("name:" name=ID)
	    ("number:" number=INT)?
	    ("vmax:" vmax=NUMBER)?
    )#
;

PinFunction:
	GPIO | I2C | SPI | UART | PWM | ADC | DAC
;

GPIO:
	type="gpio"
;

I2C:
	type=I2C_TYPE "-" bus=INT
;

I2C_TYPE:
	"sda" | "scl"
;

SPI:
	type=SPIPinType "-" bus=INT
;

SPIPinType:
	"mosi" | "miso" | "sck" | "cs"
;

UART:
	type=UARTPinType "-" bus=INT
;

UARTPinType:
	"tx" | "rx"
;

PWM:
	type="pwm-" interface=INT
;

ADC:
	type="adc"
;

DAC:
	type="dac"
;


CPU:
	(
        ("cpu_family:" cpu_family=CPUFamilyType)
	    ("max_freq:" max_freq=NUMBER unit=FrequencyUnit)
	    ("fpu:" fpu=BOOL)
    )#
;


CPUFamilyType:
	( "ESP32" | "ESP8266" | "PiArmCortex")
;

Memory:
	(
        (("ram:" ram=NUMBER UNITS?)
        ("rom:" rom=NUMBER UNITS?)?
        ("flash:" flash=NUMBER UNITS?)?)# |
        (("ram:" ram=NUMBER UNITS?)?
        ("rom:" rom=NUMBER UNITS?)?
        ("flash:" flash=NUMBER UNITS?))# |
        (("ram:" ram=NUMBER UNITS?)?
        ("rom:" rom=NUMBER UNITS?)
        ("flash:" flash=NUMBER UNITS?)?)#
    )
;

UNITS:
    ("kb" | "mb" | "gb" | "b")
;

NetworkInterface:
    WifiInterface   |
    EthernetInterface
;

WifiInterface:
	"wifi:"
	(
        ("name:" name=ID)
	    ("freq:" freq=NUMBER unit=FrequencyUnit)?
    )#
;

FrequencyUnit:
	"ghz" | "mhz" | "hz"
;

EthernetInterface:
	"ethernet:"
        "name:" name=ID
;

Bluetooth:
    'NA' |
    'BT3' |
    'BT4' |
    'BT5'
;

NID:
	/(.*?)\n/
;

FQN: ID+['.'];
FQNI: ID+['.']('.*')?;
Import: 'import' importURI=FQNI ('as' name=ID)?;

Description: /\#(.*?)$/;

// Special rule for comments. Comments start with //

// Comments
Comment: CommentLine | CommentBlock ;

CommentLine: /\/\/.*?$/;

CommentBlock: /\/\*(.|\n)*?\*\//;