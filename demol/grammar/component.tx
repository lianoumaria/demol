//Device
ComponentModel:
    component=Component
;

Component:
    Board | Peripheral
;


Board:
    'Board' name=ID
	(
	    ("cpu" ":" cpu=CPU)
        ("memory" ":" memory=Memory)
        ("vcc" ":" vcc=PowerType)
	    ("pins" ":" "-" pins*=Pin["-"])
	    ('networking' ':' '-' networking*=NetworkInterface['-'])?
	    ('bluetooth' ':' bluetooth=Bluetooth)?
        ("ioVcc" ":" iovcc=PowerType)?
     )#
    'end'
;

Peripheral: Sensor | Actuator;

Actuator:
    'Actuator' name=ID
	(
	  ("vcc" ":" vcc=PowerType)
	  ("msg:" msg=MessageType)
	  ("pins" ":" "-" pins*=Pin["-"])
	  ("attributes" ":" "-" attributes*=Attribute["-"])?
	  ("driverTpl" ":" driverTpl=STRING)?
    )#
    'end'
;

Sensor:
    'Sensor' name=ID
	(
	  ("vcc" ":" vcc=PowerType)
	  ("msg:" msg=MessageType)
	  ("pins" ":" "-" pins*=Pin["-"])
	  ("attributes" ":" "-" attributes*=Attribute["-"])?
	  ("driverTpl" ":" driverTpl=STRING)?
    )#
    'end'
;

Pin:
	PowerPin | IOPin
;

PowerPin:
	"power:"
	(
        ("name:" name=ID)
	    ("number:" number=INT)
	    ("type:" ptype=PowerType)
    )#
;

PowerType:
	"GND"   |
    "5V"    |
    "3V3"   |
    "12V"
;

IOPin:
	"io_pin:"
	(
        ("functions:" funcs*=PinFunction[","])
        ("name:" name=ID)
	    ("number:" number=INT)?
	    ("vmin:" vmin=NUMBER)?
	    ("vmax:" vmax=NUMBER)?
    )#
;

PinFunction:
	GPIO | I2C | SPI | UART | PWM | ADC | DAC
;

GPIO:
	ptype="gpio"
;

I2C:
	ptype=I2C_TYPE "-" bus=INT
;

I2C_TYPE:
	"sda" | "scl"
;

SPI:
	ptype=SPIPinType "-" bus=INT
;

SPIPinType:
	"mosi" | "miso" | "sck" | "cs"
;

UART:
	ptype=UARTPinType "-" bus=INT
;

UARTPinType:
	"tx" | "rx"
;

PWM:
	ptype="pwm" "-" channel=INT
;

ADC:
	ptype="adc"
;

DAC:
	ptype="dac"
;


CPU:
	(
        ("cpu_family:" cpu_family=CPUFamilyType)
	    ("max_freq:" max_freq=NUMBER unit=FrequencyUnit)
	    ("fpu:" fpu=BOOL)
    )#
;


CPUFamilyType:
	("ESP32" | "ESP8266" | "PiArmCortex")
;

Memory:
	(
        ("ram:" ram=NUMBER UNITS)?
        ("rom:" rom=NUMBER UNITS)?
        ("flash:" flash=NUMBER UNITS)?
    )#
;

UNITS:
    ("kb" | "mb" | "gb" | "b")
;

NetworkInterface:
    WifiInterface   |
    EthernetInterface
;

WifiInterface:
	"wifi:"
	(
        ("name:" name=ID)
	    ("freq:" freq=NUMBER unit=FrequencyUnit)?
    )#
;

FrequencyUnit:
	"ghz" | "mhz" | "hz"
;

EthernetInterface:
	"ethernet:"
        "name:" name=ID
;

Bluetooth:
    'NA' |
    'BT3' |
    'BT4' |
    'BT5'
;


MessageType:
	SensorMsgType | ActuatorMsgType
;

// Τα templates γράφονται με ωάση τα MsgType που υποστηρίζει η DSL
SensorMsgType:
	"Distance"      |
    "Temperature"   |
    "Humidity"      |
    "Gas"           |
    "Pressure"      |
    "Env"           |
    "Acceleration"  |
    "IMU"
;

ActuatorMsgType:
	"MotorController"   |
    "ServoController"   |
    "LedArray"
;


Attribute:
    NumericAttribute    |
    BoolAttribute       |
    StringAttribute     |
    ListAttribute       |
    DictAttribute
;

NumericAttribute:
    IntAttribute | FloatAttribute
;

IntAttribute:
    name=ID ':' 'int' ('=' default=INT)?
;

FloatAttribute:
    name=ID ':' 'float' ('=' default=FLOAT)?
;

StringAttribute:
    name=ID ':' 'str' ('=' default=STRING)?
;

BoolAttribute:
    name=ID ':' 'bool' ('=' default=BOOL)?
;

ListAttribute:
    name=ID ':' 'list' ('=' '[' default*=OBJECT[','] ']')?
;

DictAttribute:
    name=ID ':' 'dict' ('=' '{' items*=Attribute[','] '}')?
;

NID:
	/(.*?)\n/
;

FQN: ID+['.'];
FQNI: ID+['.']('.*')?;
Import: 'import' importURI=FQNI ('as' name=ID)?;

Description: /\#(.*?)$/;

// Special rule for comments. Comments start with //

// Comments
Comment: CommentLine | CommentBlock ;

CommentLine: /\/\/.*?$/;

CommentBlock: /\/\*(.|\n)*?\*\//;